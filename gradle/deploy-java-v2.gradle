/*
includes both artifactory and bintray upload tasks.
deploy.settings configuration file is in the projectDir:

deploy.settings:
siteUrl = https://github.com...
gitUrl = https://github.com...
groupId = com.company
id = libName
licenseName = MIT
licenseUrl = https://opensource.org/licenses/MIT

also, ~/.gradle/gradle.properties needs to define

devReadUser=
devReadPassword=
hmBintrayUser=
hmBintrayApikey=
hmArtifactoryPublishUser=
hmArtifactoryPublishPassword=
hmArtifactoryUrl=

also, ext.ver."$deploy.id" or "version" needs to be defined in a build.gradle
*/

if (project.hasProperty("hmArtifactoryUrl")) {
    apply plugin: "maven-publish"
    apply plugin: 'com.jfrog.bintray'

    def deploy = new Properties()
    deploy.load(new FileInputStream("$projectDir/deploy.settings"))

    // use either ver."$deploy.id" or version
    def deployVersion = ver."$deploy.id" ?: project.version

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        repositories {
            maven {
                name = 'artifactory'
                def repoKey = project.hasProperty('repo') ? project.repo : "gradle-dev-local"
                url = "$hmArtifactoryUrl/$repoKey"
                credentials {
                    username hmArtifactoryPublishUser
                    password hmArtifactoryPublishPassword
                }
            }
        }
        
        publications {
            mavenJava(MavenPublication) {
                from components.java

                 pom {
                    groupId = deploy.groupId
                    artifactId = deploy.id
                    version = deployVersion
                    name = deploy.id
                    url = deploy.siteUrl
                    inceptionYear = '2018'
                    licenses { 
                        license {
                            name = deploy.licenseName
                            url = deploy.licenseUrl
                        }
                    }
                    scm {
                        connection = deploy.gitUrl
                        developerConnection = deploy.gitUrl
                        url = deploy.siteUrl
                    }
                }
            }
        }
    }

    bintray {
        user = hmBintrayUser
        key = hmBintrayApikey

        publications = ['mavenJava']
        pkg {
            repo = "maven"
            name = deploy.id
            websiteUrl = deploy.siteUrl
            vcsUrl = deploy.gitUrl
            licenses = deploy.licenseName

            version {
                name = deployVersion
            }
        }
    }
}
